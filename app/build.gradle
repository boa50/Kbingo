apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "br.com.boa50.kbingo"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 10
        versionName "0.9"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix = ".debug"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    flavorDimensions "default"
    productFlavors {
        prodSpeed {
            dimension "default"
            applicationIdSuffix = ".prodSpeed"
            buildConfigField "int", "DELAY_CLICK", "0"
        }
        mock {
            dimension "default"
            applicationIdSuffix = ".mock"
            buildConfigField "int", "DELAY_CLICK", "0"
        }
        prod {
            dimension "default"
            buildConfigField "int", "DELAY_CLICK", "1000"
        }
    }

    android.variantFilter { variant ->
        if (variant.buildType.name == 'release'
                && (variant.flavors[0].name == 'mock'
                || variant.flavors[0].name == 'prodSpeed')) {
            variant.ignore = true
        }
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    buildToolsVersion '28.0.2'
    sourceSets {
        mock { java.srcDirs = ['src/mock/java', 'src/mock/java/'] }
        prod { java.srcDirs = ['src/prod/java', 'src/prod/java/'] }
        prodSpeed { java.srcDirs = ['src/prod/java', 'src/prodSpeed/java/'] }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:${supportLibraryVersion}"
    implementation "com.android.support:gridlayout-v7:${supportLibraryVersion}"
    implementation "com.android.support:cardview-v7:${supportLibraryVersion}"
    implementation "com.android.support.constraint:constraint-layout:${constraintVersion}"
    implementation "com.android.support:support-v4:${supportLibraryVersion}"
    implementation "com.android.support:design:${supportLibraryVersion}"
    implementation "com.android.support:support-annotations:${supportLibraryVersion}"
    implementation "com.android.support:recyclerview-v7:${supportLibraryVersion}"
    implementation "com.google.guava:guava:${guavaVersion}"
    implementation "com.jakewharton:butterknife:${butterknifeVersion}"
    implementation "com.google.dagger:dagger:${daggerVersion}"
    implementation "com.google.dagger:dagger-android:${daggerVersion}"
    implementation "com.google.dagger:dagger-android-support:${daggerVersion}"
    implementation "io.reactivex.rxjava2:rxjava:${rxjavaVersion}"
    implementation "io.reactivex.rxjava2:rxandroid:${rxandroidVersion}"
    implementation "android.arch.persistence.room:runtime:${roomVersion}"
    implementation "android.arch.persistence.room:rxjava2:${roomVersion}"
    testImplementation "junit:junit:${junitVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.hamcrest:java-hamcrest:${hamcrestVersion}"
    testImplementation "android.arch.persistence.room:testing:${roomVersion}"
    androidTestImplementation "com.android.support.test:runner:${testsVersion}"
    androidTestImplementation "com.android.support.test:rules:${testsVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:${espressoVersion}"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:${espressoVersion}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${butterknifeVersion}"
    annotationProcessor "com.google.dagger:dagger-compiler:${daggerVersion}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${daggerVersion}"
    annotationProcessor "android.arch.persistence.room:compiler:${roomVersion}"
}
